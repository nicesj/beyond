plugins {
    id 'de.undercouch.download'
}

apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'

def getGitHash = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments '-DCMAKE_VERBOSE_MAKEFILE=1'
                arguments '-DCMAKE_INSTALL_PREFIX:PATH=/usr'
                arguments '-DANDROID_STL=c++_shared'
                arguments '-DBUILD_GTEST=OFF', '-DENABLE_GTEST=OFF'
                arguments '-DUSE_PREBUILT=OFF'
                arguments '-DPREBUILT_PATH=""'
                arguments '-DPKG_CONFIG_USE_CMAKE_PREFIX_PATH=ON'
                arguments '-DSYSCONF_INSTALL_DIR=/etc', '-DLIB_INSTALL_DIR=/usr/lib', '-DINCLUDE_INSTALL_DIR=/usr/include', '-DDATA_INSTALL_DIR=/usr/share', '-DPKGCONFIG_DIR=/usr/lib/pkgconfig', '-DPREFIX=/usr'
                arguments '-DNAME=beyond', '-DVERSION=0.0.1'
                arguments "-DREVISION=${getGitHash()}"
                arguments '-DPLATFORM=android'
                // NOTE:
                // COVERAGE: Only could be turned on for the Debug build (CMAKE_BUILD_TYPE: Debug, gradle will choose this automatically)
                arguments '-DCOVERAGE=ON'
                // NOTE:
                // AAR does not necessary to package the tool in it
                // However, if we trigger the cmake build on the CLI, the tool will be enabled by default
                abiFilters 'arm64-v8a', 'x86'
                targets "beyond-authenticator_ssl"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            path "./CMakeLists.txt"
        }
    }

    packagingOptions {
        // NOTE:
        // Surpress the build warning
        // By the warning, the following option is going to be turned on automatically.
        //
        // The warning message was
        // 'PackagingOptions.jniLibs.useLegacyPackaging should be set to true because android:extractNativeLibs is set to "true" in AndroidManifest.xml.'
        jniLibs.useLegacyPackaging true
        exclude 'lib/*/libc++_shared.so'
        exclude 'lib/*/libbeyond.so'
    }
}

dependencies {
    compileOnly project(':libbeyond')

    implementation 'com.android.ndk.thirdparty:jsoncpp:1.8.4-alpha-1'
    implementation 'com.android.ndk.thirdparty:openssl:1.1.1g-alpha-1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.7.1"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

preBuild.dependsOn ":libbeyond:build"
